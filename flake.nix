{
  inputs = {
    nixpkgs.url = "github:nixos/nixpkgs?ref=nixos-25.11";
    nixpkgs-unstable.url = "github:nixos/nixpkgs?ref=nixos-unstable";
    flake-utils.url = "github:numtide/flake-utils";
    kernel-firecracker = {
      # Tree generated by this script:
      # https://github.com/firecracker-microvm/firecracker/pull/5637/commits/d043dc54b7326350b1c9b715cf784b06f5547d7f
      url = "github:bjackman/linux?ref=firecracker-host-2";
      flake = false;
    };
    kernel-firecracker-ephmap = {
      # Old version of the above, but with a simplistic ephmap call added to the
      # write path, with a simplistic TLB optimisation applied.
      url = "github:bjackman/linux?ref=firecracker-host+ephmap";
      flake = false;
    };
    kernel-benchmarks-nix = {
      url = "github:bjackman/kernel-benchmarks-nix";
      inputs.nixpkgs.follows = "nixpkgs";
    };
  };

  outputs =
    inputs@{
      self,
      nixpkgs,
      nixpkgs-unstable,
      flake-utils,
      kernel-benchmarks-nix,
      ...
    }:
    flake-utils.lib.eachDefaultSystem (
      system:
      let
        pkgs = import nixpkgs { inherit system; };
      in
      {
        formatter = pkgs.nixfmt-tree;

        devShells.default = pkgs.mkShell {
          packages = with pkgs; [ nixos-rebuild ];
        };
      }
    )
    // flake-utils.lib.eachDefaultSystemPassThrough (system: {
      nixosConfigurations.aethelred = nixpkgs.lib.nixosSystem {
        system = "x86_64-linux";
        modules = [
          ./modules/aethelred.nix
          kernel-benchmarks-nix.nixosModules.benchmarks.firecracker-perf-tests
          {
            # Useful for poking around for experiments
            nix.registry.nixpkgs-unstable.flake = nixpkgs-unstable;
            nixpkgs.overlays = [ self.overlays.firecracker ];
          }
        ];
        specialArgs = { inherit self; };
      };

      overlays.firecracker = (
        final: prev: {
          # "populate_latency": [1921.072, 1953.675, 1922.482, 1951.327, 1941.704, 1931.595, 1920.618, 1949.934, 1945.512, 1927.19, 1917.859, 1943.812, 1920.943, 1916.953, 1937.345, 1940.211, 1921.318, 1926.616, 1911.107, 1936.172, 1947.748, 1921.792, 1945.138, 1935.245, 1919.356, 1927.065, 1951.687, 1920.913, 1941.564, 1954.387]
          linuxPackages_firecracker = prev.linuxPackages_custom {
            version = "6.19.0-rc5";
            src = inputs.kernel-firecracker;
            # Use a massive config since that's the only thing I know makes Docker work.
            # This one has at least been through a make localmodconfig dance.
            configfile = ./kconfigs/v6.19_nix_big.config;
          };
          #  "populate_latency": [2056.114, 2060.27, 2040.085, 2060.202, 2075.324, 2033.987, 2057.317, 2035.927, 2058.438, 2056.828, 2061.481, 2031.189, 2029.544, 2040.335, 2032.227, 2041.634, 2041.136, 2061.472, 2066.608, 2053.442, 2038.645, 2062.134, 2036.871, 2062.678, 2056.329, 2057.52, 2056.166, 2056.378, 2061.378, 2063.784]}
          linuxPackages_firecracker-ephmap = prev.linuxPackages_custom {
            version = "6.17.0-rc3";
            src = inputs.kernel-firecracker-ephmap;
            # Use a massive config since that's the only thing I know makes Docker work.
            configfile = ./kconfigs/v6.17_nix_big.config;
          };
        }
      );
    });
}
