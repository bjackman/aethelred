{
  inputs = {
    nixpkgs.url = "github:nixos/nixpkgs?ref=nixos-25.11";
    nixpkgs-unstable.url = "github:nixos/nixpkgs?ref=nixos-unstable";
    flake-utils.url = "github:numtide/flake-utils";
    kernel-firecracker = {
      # Tree generated by this script:
      # https://github.com/firecracker-microvm/firecracker/pull/5637/commits/653b4becc6256a241fed0cc42845fae471f3eefe
      # (At that commit of the firecracker tree)
      # Then rebased onto a linux-next, with a couple of fixups and a new modparam.
      # This is coupled with the exact version of firecracker used below.
      url = "github:bjackman/linux?ref=firecracker-host-4";
      flake = false;
    };
    kernel-gfp_unmapped = {
      # Similar to above, but merged with my __GFP_UNMAPPED support
      url = "github:bjackman/linux?ref=firecracker-host-4+page_alloc-unmapped";
      flake = false;
    };
    kernel-benchmarks-nix = {
      # Using an experimental new version of this thing.
      url = "github:bjackman/kernel-benchmarks-nix?ref=v2";
      inputs.nixpkgs.follows = "nixpkgs";
      # Pin firecracker to a version that works with the kernels above.
      inputs.firecracker.url = "github:firecracker-microvm/firecracker?rev=d043dc54b7326350b1c9b715cf784b06f5547d7f";
    };
  };

  outputs =
    inputs@{
      self,
      nixpkgs,
      nixpkgs-unstable,
      flake-utils,
      kernel-benchmarks-nix,
      ...
    }:
    flake-utils.lib.eachDefaultSystem (
      system:
      let
        pkgs = import nixpkgs { inherit system; };
      in
      {
        formatter = pkgs.nixfmt-tree;

        devShells.default = pkgs.mkShell {
          packages = with pkgs; [ nixos-rebuild ];
        };
      }
    )
    // flake-utils.lib.eachDefaultSystemPassThrough (system: {
      nixosConfigurations =
        let
          mkConfig =
            extraModules:
            nixpkgs.lib.nixosSystem {
              system = "x86_64-linux";
              modules = [
                ./modules/aethelred.nix
                kernel-benchmarks-nix.nixosModules.default
                kernel-benchmarks-nix.nixosModules.benchmarks.firecracker-perf-tests
                {
                  # Useful for poking around for experiments
                  nix.registry.nixpkgs-unstable.flake = nixpkgs-unstable;
                  nixpkgs.overlays = [ self.overlays.firecracker ];
                }
              ]
              ++ extraModules;
              specialArgs = { inherit self; };
            };
          kernel-firecracker = { pkgs, ... }: {
            boot.kernelPackages = pkgs.linuxPackages_firecracker;
          };
          kernel-gfp_unmapped = { pkgs, ... }: {
            boot.kernelPackages = pkgs.linuxPackages_gfp_unmapped;
            system.nixos.variantName = "gfp_unmapped";
          };
        in
        {
          base = mkConfig [
            kernel-firecracker
            {
              boot.kernelParams = [ "kvm.guest_memfd_tlb_flush=1" ];
            }
          ];
          skip-flush = mkConfig [
            kernel-firecracker
            {
              boot.kernelParams = [ "kvm.guest_memfd_tlb_flush=0" ];
              system.nixos.variantName = "skip-flush";
            }
          ];
          gfp_unmapped = mkConfig [ kernel-gfp_unmapped ];
        };

      overlays.firecracker = (
        final: prev: {
          linuxPackages_firecracker = prev.linuxPackages_custom {
            version = "6.19.0-rc4-next-20260109";
            src = inputs.kernel-firecracker;
            # Use a massive config since that's the only thing I know makes Docker work.
            # This one has at least been through a make localmodconfig dance.
            configfile = ./kconfigs/v6.19_nix_big.config;
          };
          linuxPackages_gfp_unmapped = prev.linuxPackages_custom {
            version = "6.19.0-rc4-next-20260109";
            src = inputs.kernel-gfp_unmapped;
            # Use a massive config since that's the only thing I know makes Docker work.
            # This one has at least been through a make localmodconfig dance.
            configfile = ./kconfigs/v6.19_nix_big.config;
          };
        }
      );
    });
}
